plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'cl.franciscosolis.blossom-extended' version '1.3.1'
    id 'org.jetbrains.dokka' version '1.7.20'
}

def projectVersion = (System.getenv("VERSION") ?: '0.1.6-SNAPSHOT').replaceFirst("v", "").replace('/', '')

group 'xyz.theprogramsrc'
version projectVersion
description 'Translations module for SimpleCore API.'

repositories {
    mavenLocal()
    mavenCentral()

    maven { url 'https://repo.theprogramsrc.xyz/repository/maven-public/' }
    maven { url 'https://repo.theprogramsrc.xyz/repository/simplecoreapi-modules/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib:1.7.22'
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'xyz.theprogramsrc:simplecoreapi:0.3.1-SNAPSHOT'
    compileOnly 'xyz.theprogramsrc:filesmodule:0.1.1-SNAPSHOT'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

shadowJar {
    mergeServiceFiles()
    exclude('**/*.kotlin_metadata')
    exclude('**/*.kotlin_builtins')

    archiveBaseName.set('TranslationsModule')
    archiveClassifier.set('')
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
}

blossom {
    replaceToken '@name@', rootProject.name
    replaceToken '@version@', project.version.toString()
    replaceToken '@description@', project.description
    replaceToken '@git_short@', System.getenv("GIT_COMMIT_SHORT_HASH") ?: "unknown"
    replaceToken '@git_full@', System.getenv("GIT_COMMIT_LONG_HASH") ?: "unknown"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
    testImplementation {
        extendsFrom(compileOnly)
    }
}

tasks.named("dokkaHtml") {
    outputDirectory.set(file(project.buildDir.absolutePath + '/dokka'))
}

publishing {
    repositories {
        if(System.getenv('env') == 'prod'){
            maven {
                name = 'GitHubPackages'
                url = 'https://maven.pkg.github.com/TheProgramSrc/SimpleCore-TranslationsModule'
                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }else{
            mavenLocal()
        }
    }
    publications {
        mavenKotlin(MavenPublication) { publication ->
            artifactId = 'translationsmodule'

            from components.java

            pom.withXml {
                asNode().appendNode('packaging', 'jar')
                if(asNode().get('dependencies')[0] != null) {
                    asNode().remove(asNode().get('dependencies')[0])
                }
            }
        }
    }
}

publish.dependsOn clean, test, jar, shadowJar
